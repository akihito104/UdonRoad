plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}
apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    signingConfigs {
        release {
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASS")
            storeFile file("${System.getenv("STORE_FILE")}")
            storePassword System.getenv("STORE_PASS")
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        // change for your app
        applicationId "com.freshdigitable.udonroad"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 12
        versionName "0.2.6"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "com.freshdigitable.udonroad.MockTestRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug", appIcon: "@mipmap/ic_launcher_debug"]
            debuggable true
// https://code.google.com/p/android/issues/detail?id=206259
//            shrinkResources true
            zipAlignEnabled true
        }
        release {
            manifestPlaceholders = [appName: "@string/app_name", appIcon: "@mipmap/ic_launcher"]
            debuggable false
            shrinkResources false
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
    }

    realm {
        syncEnabled false
    }
}

def supportLibVer = '27.0.2'
def androidArchVer = '1.0.0'
def t4jVer = '4.0.6'
def rxVer = '2.1.5'
def rxAndroidVer = '2.0.2'
def daggerVer = "2.15"
def supportTestVer = "1.0.1"
def espressoVer = "3.0.1"
def dexmakerVer = '2.16.0'
def leakcanaryVer= '1.5.4'
def okhttpVer = '3.10.0'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportLibVer"
    implementation "com.android.support:recyclerview-v7:$supportLibVer"
    implementation "com.android.support:design:$supportLibVer"
    implementation "com.android.support:preference-v7:$supportLibVer"
    implementation "com.android.support:preference-v14:$supportLibVer"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$androidArchVer"
    annotationProcessor "android.arch.lifecycle:compiler:$androidArchVer"
    implementation "android.arch.lifecycle:common-java8:$androidArchVer"

    implementation "org.twitter4j:twitter4j-core:$t4jVer"
    implementation "org.twitter4j:twitter4j-stream:$t4jVer"
    implementation "org.twitter4j:twitter4j-http2-support:$t4jVer"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "com.squareup.okhttp3:okhttp:$okhttpVer"

    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVer"
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation "io.reactivex.rxjava2:rxjava:$rxVer"

    implementation "com.google.dagger:dagger:$daggerVer"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVer"
    implementation "com.google.dagger:dagger-android:$daggerVer"
    implementation "com.google.dagger:dagger-android-support:$daggerVer"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVer"

    implementation 'com.jakewharton.timber:timber:4.7.0'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVer"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVer"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVer"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:2.0.2-beta'
    androidTestImplementation "com.android.support.test:runner:$supportTestVer"
    androidTestImplementation "com.android.support.test:rules:$supportTestVer"
    androidTestImplementation "android.arch.core:core-testing:$androidArchVer"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVer"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espressoVer") {
        transitive = false
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVer"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVer"

    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$dexmakerVer"
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVer"
}

configurations.all { config ->
    if (config.name.contains('UnitTest') || config.name.contains('AndroidTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}

check.dependsOn(checkLicenses)

project.afterEvaluate {
    generateDebugAssets.dependsOn (generateLicensePage)
    generateReleaseAssets.dependsOn (generateLicensePage)
}
